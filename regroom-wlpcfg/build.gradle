apply plugin: 'eclipse'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.6'
    }

}

def dockerHost = "https://localhost:2376"
if (null != System.getenv("DOCKER_HOST"))
{
    dockerHost = System.getenv('DOCKER_HOST')

    if (dockerHost.indexOf('tcp') == 0)
    {
        dockerHost = dockerHost.replace('tcp','https')
    }
}

def dockerCertPath = ""
if (null != System.getenv('DOCKER_CERT_PATH')){
    dockerCertPath = System.getenv('DOCKER_CERT_PATH')
}

def localEnvironment = ["LICENSE=accept",
            "KAFKA_URL=" + System.getenv('KAFKA_URL'),
            "KAFKA_USER=" + System.getenv('KAFKA_USER'),
            "KAFKA_PASSWORD=" + System.getenv('KAFKA_PASSWORD'),
        ]

if (null != System.getenv("SSL_CERT"))
{
    localEnvironment << "SSL_CERT=" + System.getenv("SSL_CERT")
}

def etcdEnvironment = ["LICENSE=accept", "ETCDCTL_ENDPOINT=http://etcd:4001"]

task copyWAR(type: Copy) {
    from '../board-app/build/libs/regroom-app-1.0.war'
    into 'servers/regroom/apps/'
    rename("regroom-app-1.0.war", "regroom-app.war")
}

task build(dependsOn: ['copyWAR']){
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

docker {
      url = dockerHost
      certPath = new File(dockerCertPath)
}

task buildDockerImage(type: DockerBuildImage, dependsOn: ['copyWAR']) {
    inputDir = file('.')
    tag = 'regroom'
    quiet = false
}

task stopCurrentContainer(type: DockerStopContainer) {
    targetContainerId { "regroom" }
    timeout 0
}

task removeCurrentContainer(type: DockerRemoveContainer) {
    targetContainerId { "regroom" }
}

task createNewContainer(type: DockerCreateContainer) {
    targetImageId { "regroom" }
    containerName = "regroom"
    portBindings = ['7040:9080','7443:9443']
    env = localEnvironment
}

task startNewContainer(type: DockerStartContainer) {
    dependsOn createNewContainer
    targetContainerId { "regroom" }
}


task createNewEtcdContainer(type: DockerCreateContainer) {
    targetImageId { "regroom" }
    containerName = "regroom"
    portBindings = ['7040:9080','7443:7443']
    env = etcdEnvironment
    links = ['etcd:etcd']
}

task startNewEtcdContainer(type: DockerStartContainer) {
    dependsOn createNewEtcdContainer
    targetContainerId { "regroom" }
}
